#version 460

layout (local_size_x_id = 0, local_size_y_id = 1, local_size_z_id = 2) in;

layout (push_constant) uniform PushConstant {
    int kernelSize;
} pc;

layout (binding = 0) uniform sampler srcSampler;
layout (binding = 1) uniform texture2D srcTex;
layout (binding = 2, rgba8) uniform writeonly image2D dstImage;
layout (binding = 3, std140) uniform UBO {
    vec4[16 / 4] weights;  // WARNING: forced 4bytes padding for each element in array
} ubo;
layout (binding = 4, std430) buffer SSBO {
    float writeBackWeights[];
} ssbo;

void main() {
    ivec2 dstIdx = ivec2(gl_GlobalInvocationID.xy);

    int kSize = pc.kernelSize;
    int halfKSize = kSize / 2;
    if (dstIdx.x <= halfKSize) {
        int weightY = dstIdx.x >> 2;
        int weightX = dstIdx.x & (4 - 1);
        ssbo.writeBackWeights[dstIdx.x] = ubo.weights[weightY][weightX];
    }

    ivec2 dstSize = imageSize(dstImage);
    if (dstIdx.x >= dstSize.x || dstIdx.y >= dstSize.y) {
        return;
    }

    vec4 acc = vec4(0.0);
    for (int y = -halfKSize; y <= halfKSize; y++) {
        vec4 rowAcc = vec4(0.0);

        for (int x = -halfKSize; x <= halfKSize; x++) {
            ivec2 inCoord = dstIdx + ivec2(x, y);
            vec4 srcVal = texture(sampler2D(srcTex, srcSampler), (vec2(inCoord) + 0.5) / vec2(dstSize));
            int absX = abs(x);
            int weightY = absX >> 2;
            int weightX = absX & (4 - 1);
            float weight = ubo.weights[weightY][weightX];
            rowAcc += srcVal * weight;
        }

        int absY = abs(y);
        int weightY = absY >> 2;
        int weightX = absY & (4 - 1);
        float weight = ubo.weights[weightY][weightX];
        acc += rowAcc * weight;
    }

    imageStore(dstImage, dstIdx, vec4(acc.rgb, 1.0));
}
