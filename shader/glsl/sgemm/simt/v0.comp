// Naive

#version 460

layout (local_size_x_id = 0, local_size_y_id = 1) in;

layout (binding = 0, std430) readonly buffer SrcMatA { float srcMatA[]; };
layout (binding = 1, std430) readonly buffer SrcMatB { float srcMatB[]; };
layout (binding = 2, std430) writeonly buffer DstMat { float dstMat[]; };

layout (constant_id = 2) const uint M = 0;
layout (constant_id = 3) const uint N = 0;
layout (constant_id = 4) const uint K = 0;

void main() {
    const uvec2 globalID = gl_GlobalInvocationID.xy;

    float acc = 0.0;
    for (uint iterK = 0; iterK < K; iterK++) {
        const uint globalIndexA = globalID.y * K + iterK;
        const float valA = srcMatA[globalIndexA];

        const uint globalIndexB = iterK * N + globalID.x;
        const float valB = srcMatB[globalIndexB];

        acc = fma(valA, valB, acc);
    }

    const uint dstIndex = globalID.y * N + globalID.x;
    dstMat[dstIndex] = acc;
}
