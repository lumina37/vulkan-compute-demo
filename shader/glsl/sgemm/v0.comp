#version 460

layout (local_size_x_id = 0, local_size_y_id = 1) in;

layout (binding = 0, std430) readonly buffer SrcMatA { float srcMatA[]; };
layout (binding = 1, std430) readonly buffer SrcMatB { float srcMatB[]; };
layout (binding = 2, std430) writeonly buffer DstMat { float dstMat[]; };

layout (constant_id = 2) const int M = 0;
layout (constant_id = 3) const int N = 0;
layout (constant_id = 4) const int K = 0;

void main() {
    const ivec2 globalID = ivec2(gl_GlobalInvocationID.xy);

    if (globalID.x >= N || globalID.y >= M) {
        return;
    }

    float acc = 0.0;
    for (int i = 0; i < K; i++) {
        const int indexA = globalID.y * K + i;
        const float valA = srcMatA[indexA];

        const int indexB = i * N + globalID.x;
        const float valB = srcMatB[indexB];

        acc += valA * valB;
    }

    const int indexDst = globalID.y * N + globalID.x;
    dstMat[indexDst] = acc;
}