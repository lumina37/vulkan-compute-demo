// Kogge-Stone prefix sum

#version 460

layout(local_size_x_id = 0) in;

layout(binding = 0, std430) readonly buffer SrcArr { float srcArr[]; };
layout(binding = 1, std430) writeonly buffer DstArr { float dstArr[]; };

layout (constant_id = 0) const uint GROUP_SIZE = 1;
layout (constant_id = 1) const uint N = 0;

shared float sharedArr[GROUP_SIZE];

void main() {
    uint localIndex = gl_LocalInvocationIndex;
    uint globalIndex = gl_GlobalInvocationID.x;
    uint groupSize = gl_WorkGroupSize.x;

    sharedArr[localIndex] = globalIndex < N ? srcArr[globalIndex] : 0;
    barrier();

    for (int step = 1; step < groupSize; step *= 2) {
        float val = 0;
        if (localIndex >= step && globalIndex < N) {
            val = sharedArr[localIndex - step];
        }
        barrier();

        sharedArr[localIndex] += val;
        barrier();
    }

    if (globalIndex < N) {
        dstArr[globalIndex] = sharedArr[localIndex];
    }
}
